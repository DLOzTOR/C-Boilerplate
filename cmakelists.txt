cmake_minimum_required(VERSION 3.16)
project(WinApiHelloWorld)

# Specify C standard
set(CMAKE_C_STANDARD 11)
set(CMAKE_C_STANDARD_REQUIRED ON)

# Add source headers
set(SRC_DIR "${CMAKE_SOURCE_DIR}/src")
set(RESOURCE_DIR "${CMAKE_SOURCE_DIR}/resources")
set(DEST_DIR "${CMAKE_BINARY_DIR}/resources")

# Gather all resource files
file(GLOB RESOURCE_FILES "${RESOURCE_DIR}/*")

# Add custom command to copy resources if changed
foreach(RESOURCE_FILE ${RESOURCE_FILES})
  get_filename_component(FILE_NAME ${RESOURCE_FILE} NAME)
  set(DEST_FILE "${DEST_DIR}/${FILE_NAME}")

  add_custom_command(
    OUTPUT ${DEST_FILE}
    COMMAND ${CMAKE_COMMAND} -E copy_if_different ${RESOURCE_FILE} ${DEST_FILE}
    DEPENDS ${RESOURCE_FILE}
    COMMENT "Copying ${FILE_NAME} if it has changed"
  )

  # Append the output file to a list of copied files
  list(APPEND COPIED_FILES ${DEST_FILE})
endforeach()

# Create a target for copying all resources
add_custom_target(copy_resources ALL DEPENDS ${COPIED_FILES})

# Add executable
add_executable(program ${SRC_DIR}/main.c)

# Add include directories to the executable
target_include_directories(program PRIVATE ${SRC_DIR})
